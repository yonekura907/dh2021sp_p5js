# 3D


size()にP3Dを記述。3Dモードに変更するとZ軸が指定できる

```
size(500, 500, P3D);
```
&nbsp;
&nbsp;


### 3Dの回転 rotate

```
// 回転の角度
int angle = 30;

void setup() {
    size(500, 500, P3D);
    background(0);
}


void draw() {    
    background(0);
    lights();
    
    // 座標変換で画面の中央を基点に
    translate(width/2, height/2, 100);
    
    // 回転
    rotateX(radians(angle));
    // rotateY(radians(angle));
    // rotateZ(radians(angle));
    
    noStroke();
    fill(0,255,0);
    
    rectMode(CENTER); // 長方形の中心をセンターに
    rect(0,0,100,100 );
    
    //noFill();
    //stroke(255);
    //box(100);
    //sphere(100);

    angle++;
    if(angle > 360){
        angle = 0;
    }
}


```
![image](https://yonekura907.github.io/dh17processing/sketch_3d01.png)

&nbsp;
&nbsp;

### Z座標を使ったアニメーション


```
int NUM = 100;

float[] x = new float[NUM]; // X座標
float[] y = new float[NUM]; // Y座標
float[] z = new float[NUM]; // Z座標


void setup(){
    size(500,500,P3D);
    // 初期配置
    for(int i = 0; i < NUM; i++){
        x[i] = random(width); // X座標:画面の中央
        y[i] = random(height); // Y座標:画面の中央
        z[i] = i * -10;
     }
}

void draw(){
    background(0); //背景白
    stroke(255); // 罫線の色
    
    for(int i = 0; i < NUM; i++){
        // lineはｚ軸で長さを出す
        line(x[i], y[i], z[i], x[i], y[i], z[i]-40);
        
        // アニメーション　Z座標が20ずつ手前に移動
        z[i] = z[i] + 20;

        // Z座標が0以上になったらランダムで再配置する。
        if(z[i] > 0){
            x[i] = random(width);
            y[i] = random(height);
            z[i] = -1000;
        }
    }
}
```

![image](https://yonekura907.github.io/dh17processing/sketch_3d02.png)

&nbsp;
&nbsp;

### 線をランダムに配置して回転

```
int NUM = 1000;

float[] startX = new float[NUM];
float[] startY = new float[NUM];
float[] startZ = new float[NUM];
float[] endX = new float[NUM];
float[] endY = new float[NUM];
float[] endZ = new float[NUM];

color[] col = new color[NUM];
float angle = 0.000;


void setup() {
    size(500, 500, P3D);
    frameRate(60);
    colorMode(HSB,360,100,100,100);

    for (int cnt = 0; cnt < NUM; cnt++) {
        startX[cnt] = random(-width/2, height/2);
        startY[cnt] = random(-width/2, height/2);
        startZ[cnt] = random(-width/2, height/2);
        endX[cnt] = random(-width/2, height/2);
        endY[cnt] = random(-width/2, height/2);
        endZ[cnt] = random(-width/2, height/2);
        col[cnt] = color(random(240,360), 100, 100,50);
    }
}

void draw() {
    
    background(0,100,0);
    noFill();

    translate(width/2, height/2);
    rotateX(angle);
    // rotateY(angle);

    for (int cnt = 0; cnt < NUM; cnt++) {
        stroke(col[cnt]);
        line(startX[cnt], startY[cnt], startZ[cnt], endX[cnt], endY[cnt], endZ[cnt]);
    }
    angle = angle + 0.001;
}

```

![image](https://yonekura907.github.io/dh17processing/sketch_3d03.png)

&nbsp;
&nbsp;